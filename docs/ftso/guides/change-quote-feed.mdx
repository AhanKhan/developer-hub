---
title: Change quote feed
tags: [intermediate, ftso, solidity]
slug: change-quote-feed
description: Change block-latency quote feeds using Solidity.
keywords:
  [
    ftso,
    oracle,
    flare-time-series-oracle,
    flare-network,
    smart-contracts,
    solidity,
    remix,
  ]
sidebar_position: 3
---

import Remix from "@site/src/components/remix";
import SetEvmVersionRemix from "/static/img/set-evm-version-remix.png";

This guide will show you how to fetch the latest feed values for two feeds and convert them to a new quote feed. For example, if you need the price of `BTC/ETH`, you can fetch the latest feed values for `BTC/USD` and `ETH/USD` and calculate the price of `BTC/ETH = (BTC/USD) / (ETH/USD)`.

In the following smart contract, for `BTC/USD` (feed index `2`) and `ETH/USD` (feed index `9`), returning the value of `BTC/ETH`, the function signature would be:

```solidity
getNewQuoteFeedValue([2, 9])
```

```solidity title="FtsoV2ChangeQuoteFeed.sol"
// SPDX-License-Identifier: MIT
pragma solidity <0.9.0;

import {IFlareContractRegistry} from "@flarenetwork/flare-periphery-contracts/coston2/util-contracts/userInterfaces/IFlareContractRegistry.sol";
import {IFastUpdater} from "@flarenetwork/flare-periphery-contracts/coston2/ftso/userInterfaces/IFastUpdater.sol";

/**
 * THIS IS AN EXAMPLE CONTRACT USING HARDCODED VALUES.
 * DO NOT USE THIS CODE IN PRODUCTION.
 */
contract FtsoV2ChangeQuoteFeed {
    IFlareContractRegistry internal contractRegistry;
    IFastUpdater internal ftsoV2;

    /**
     * Constructor initializes the contract registry and fetches the FTSOv2 contract address.
     */
    constructor() {
        contractRegistry = IFlareContractRegistry(
            0xaD67FE66660Fb8dFE9d6b1b4240d8650e30F6019
        );
        ftsoV2 = IFastUpdater(
            contractRegistry.getContractAddressByName("FastUpdater")
        );
    }

    /**
     * @dev Internal function to scale the base feed value to match the decimals of the quote feed.
     */
    function _scaleBaseFeedValue(
        uint256 _baseFeedValue,
        uint8 _baseFeedDecimals,
        uint8 _quoteDecimals
    ) internal pure returns (uint256) {
        if (_baseFeedDecimals < _quoteDecimals) {
            // Scale up if base feed decimals are less than quote feed decimals
            return
                _baseFeedValue *
                10 ** uint256(_quoteDecimals - _baseFeedDecimals);
        } else if (_baseFeedDecimals > _quoteDecimals) {
            // Scale down if base feed decimals are more than quote feed decimals
            return
                _baseFeedValue /
                10 ** uint256(_baseFeedDecimals - _quoteDecimals);
        } else {
            // No scaling needed if decimals are equal
            return _baseFeedValue;
        }
    }

    /**
     * @dev Function to compute the new quote feed value based on the base and quote feed values.
     * @param _baseAndQuoteFeedIndexes Array containing the indexes of the base and quote feeds.
     * @return The computed new quote feed value.
     */
    function getNewQuoteFeedValue(
        uint256[] calldata _baseAndQuoteFeedIndexes
    ) external view returns (uint256) {
        require(
            _baseAndQuoteFeedIndexes.length == 2,
            "Invalid feed indexes. Please provide exactly two indexes."
        );
        // Fetch current feeds
        (uint256[] memory feedValues, int8[] memory decimals, ) = ftsoV2
            .fetchCurrentFeeds(_baseAndQuoteFeedIndexes);
        uint8 newQuoteDecimals = uint8(decimals[1]);
        // Scale the base feed value to match the quote feed decimals
        uint256 scaledBaseFeedValue = _scaleBaseFeedValue(
            feedValues[0],
            uint8(decimals[0]),
            newQuoteDecimals
        );
        // Prevent division by zero
        require(feedValues[1] != 0, "Division by zero");
        // Compute the new quote feed value
        uint256 newQuoteFeedValue = (scaledBaseFeedValue *
            10 ** uint256(newQuoteDecimals)) / feedValues[1];
        return newQuoteFeedValue;
    }
}
```

{/* prettier-ignore */}
<Remix href="https://remix.ethereum.org/#version=builtin&optimize=true&runs=200&gist=c1dccb7c1216aa5f8de7caf03565d8d2">Open in Remix</Remix>

<br></br>

:::warning[When compiling and deploying the contract]

- **Using Remix:** Set EVM version to `london` in the **Advanced Configurations** section of the **Solidity Compiler** tab:

  <img src={SetEvmVersionRemix} style={{ width: 300 }} />

- **Using Standard Solidity JSON:** Set `evmVersion` to `london`:

  ```json
  {
    "settings": {
      "optimizer": {
        /* ... */
      },
      "evmVersion": "london"
    }
  }
  ```

- **Using `solc` CLI:** Set `--evm-version` to `london`:

  ```bash
  solc --evm-version london <args>
  ```

:::

<details>
<summary>Didn't understand the Solidity code?</summary>

- **Purpose**: This contract interacts with the Flare Network to fetch feed values and compute a new quote feed value based on the base and quote feed values.

- **Dependencies**:

  - `IFlareContractRegistry`: Interface to get contract addresses from the Flare Network registry.
  - `IFastUpdater`: Interface to fetch current feed values.

- **State Variables**:

  - `contractRegistry`: Instance of `IFlareContractRegistry` to access the contract registry.
  - `ftsoV2`: Instance of `IFastUpdater` to fetch feed values.

- **Constructor**:

  - Initializes `contractRegistry` with a hardcoded address.
  - Retrieves and sets the `ftsoV2` contract address using the contract registry.

- **Internal Function `_scaleBaseFeedValue`**:

  - Scales the base feed value to match the decimals of the quote feed.
  - Adjusts the feed value by scaling up or down depending on the difference in decimals.

- **External Function `getNewQuoteFeedValue`**:

  - Accepts an array of feed indexes (base and quote feed indexes).
  - Fetches current feed values and decimals using `ftsoV2`.
  - Scales the base feed value to match the quote feed decimals.
  - Computes and returns the new quote feed value by adjusting for the decimal differences and performing a calculation.

- **Key Validations**:
  - Ensures exactly two feed indexes are provided.
  - Prevents division by zero during the computation of the new quote feed value.

</details>
