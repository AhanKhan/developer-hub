---
slug: getting-started
title: Getting Started
description: Learn how to consume FTSOv2 feeds on Flare using an on-chain Solidity contract.
keywords:
  [
    ftso,
    oracle,
    flare-time-series-oracle,
    flare-network,
    smart-contracts,
    solidity,
    remix,
    metamask,
  ]
---

import Remix from "@site/src/components/remix";
import OpenSolidityCompiler from "/static/img/ftso-getting-started/0-open-solidity-compiler-tab.png";
import SetEvmVersion from "/static/img/ftso-getting-started/1-set-evm-version.png";
import SetInjectedProvider from "/static/img/ftso-getting-started/1-set-injected-provider.png";
import CompileContract from "/static/img/ftso-getting-started/2-compile-contract.png";
import OpenDeploy from "/static/img/ftso-getting-started/3-open-deploy.png";
import DeployContract from "/static/img/ftso-getting-started/4-deploy-contract.png";
import ConfirmDeployInMetamask from "/static/img/ftso-getting-started/5-confirm-deploy-in-metamask.png";
import QueryLatestFeeds from "/static/img/ftso-getting-started/6-query-latest-feeds.png";

You can use FTSOv2 to connect your smart contracts with real-world data feeds. FTSOv2 leverages Flare's network of 100 independent data providers to fetch off-chain data and deliver it on-chain. This section demonstrates how to consume FTSOv2's block-latency feeds on Flare using an on-chain Solidity contract.

:::info
If you are new to smart contract development, learn how to [deploy your first smart contract](/network/getting-started) before you start this guide.
:::

## Sample contract

This example smart contract queries the latest feed values for FLR/USD, BTC/USD, and ETH/USD from FTSOv2 on Flare Testnet Coston2.

```solidity title="FtsoV2FeedConsumer.sol"
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0 <0.9.0;

import {IFlareContractRegistry} from "@flarenetwork/flare-periphery-contracts/coston2/util-contracts/userInterfaces/IFlareContractRegistry.sol";
import {IFastUpdater} from "@flarenetwork/flare-periphery-contracts/coston2/ftso/userInterfaces/IFastUpdater.sol";

/**
 * THIS IS AN EXAMPLE CONTRACT USING HARDCODED VALUES.
 * DO NOT USE THIS CODE IN PRODUCTION.
 */

contract FtsoV2FeedConsumer {
    IFlareContractRegistry internal contractRegistry;
    IFastUpdater internal ftsoV2;
    // Feed indexes: 0 = FLR/USD, 2 = BTC/USD, 9 = ETH/USD
    uint256[] public feedIndexes = [0, 2, 9];

    constructor() {
        contractRegistry = IFlareContractRegistry(0xaD67FE66660Fb8dFE9d6b1b4240d8650e30F6019);
        ftsoV2 = IFastUpdater(contractRegistry.getContractAddressByName("FastUpdater"));
    }

    /**
     * Get the current value of the feeds.
     */
    function getFtsoV2CurrentFeedValues()
        external
        view
        returns (uint256[] memory _feedValues, int8[] memory _decimals, int64 _timestamp)
    {
        (
            uint256[] memory feedValues,
            int8[] memory decimals,
            int64 timestamp
        ) = ftsoV2.fetchCurrentFeeds(feedIndexes);
        /* Your custom feed consumption logic. */
        /* In this example the feed values, decimals and last updated timestamp are just returned. */
        return (feedValues, decimals, timestamp);
    }
}
```

{/* prettier-ignore */}
<Remix href="https://remix.ethereum.org/#version=builtin&optimize=true&runs=200&gist=d6181b71e7932701efbc6c3c78898f5a">Open in Remix</Remix>

### Breaking down the contract

- **Purpose**: Interacts with the Flare Network to fetch current feed values for specific cryptocurrency pairs (FLR/USD, BTC/USD, ETH/USD).

- **Dependencies**:

  - `IFlareContractRegistry`: Interface to get contract addresses from the Flare Network registry.
  - `IFastUpdater`: Interface to fetch current feed values.

- **State Variables**:

  - `contractRegistry`: Instance of `IFlareContractRegistry` to access the contract registry.
  - `ftsoV2`: Instance of `IFastUpdater` to fetch feed values.
  - `feedIndexes`: Array of feed indexes ([0, 2, 9]) representing FLR/USD, BTC/USD, ETH/USD.

- **Constructor**:

  - Initializes `contractRegistry` with a hardcoded address. This address is the same on mainnet and testnet.
  - Retrieves and sets the `ftsoV2` contract address using the contract registry.

- **Function `getFtsoV2CurrentFeedValues`**:
  - Fetches the current feed values, decimals, and timestamp.
  - Returns the fetched values.

## Compile, deploy, and run the contract

:::info
If you have not already configured your MetaMask wallet to support Flare Testnet Coston2 and funded it with testnet C2FLR, learn how to [deploy your first smart contract](/network/getting-started).

You can get testnet C2FLR from the [Coston2 Faucet](https://faucet.flare.network/coston2).
:::

{/* prettier-ignore */}
1. <Remix href="https://remix.ethereum.org/#version=builtin&optimize=true&runs=200&gist=d6181b71e7932701efbc6c3c78898f5a">Open contract in Remix</Remix>

2. Click on `FtsoV2FeedConsumer.sol` in the file explorer to open the contract in the editor.

2. On the left side of Remix, click the **Solidity Compiler** tab to view the compiler settings.

   <img src={OpenSolidityCompiler} style={{ width: 300 }} />

3. Expand the **Advanced Configurations** section and set the **EVM Version** to `london`.

   <img src={SetEvmVersion} style={{ width: 300 }} />

3. Click the **Compile FtsoV2FeedConsumer.sol** button to compile the contract.

    <img src={CompileContract} style={{ width: 300 }} />

4. On the left side of Remix, click the **Deploy & Run transactions** tab to view the deployment settings.

   <img src={OpenDeploy} style={{ width: 300 }} />

5. Select the **Injected Provider - MetaMask** environment.

   <img src={SetInjectedProvider} style={{ width: 300 }} />

6. Click **Deploy** to deploy the contract to Flare Testnet Coston2. MetaMask opens and asks you to confirm payment for deploying the contract. Make sure MetaMask is set to Flare Testnet Coston2 before you confirm the transaction.

    <img src={DeployContract} style={{ width: 300 }} />

7. In the MetaMask prompt, click **Confirm** to approve the transaction and spend your testnet C2FLR required to deploy the contract.

    <img src={ConfirmDeployInMetamask} style={{ width: 300 }} />

8. After a few seconds, the transaction completes and your contract appears under the **Deployed/Unpinned Contracts** list in Remix. Click the contract dropdown to view its variables and functions. Click on **getFtsoV2CurrentFeedValues** to show the latest feed values and decimals.

    <img src={QueryLatestFeeds} style={{ width: 300 }} />

- The `_feedValues` array will contain the current integer values of FLR/USD, BTC/USD, and ETH/USD in the same order as the feed indexes were given.
- The `_decimals` array will contain the decimal places for the corresponding feeds.
- The `_timestamp` variable will contain the timestamp of the last feed update.

The floating point price of a feed can be calculated by dividing the `feedValue` by 10^`decimals`. For example, if the feed value of BTC/USD is `6900420` and the decimal is `2`, the floating point feed value is `69004.20`.

:::tip[What's next?]
Learn about which all block-latency feeds you query on-chain using FTSOv2, along with their corresponding feed indexes.
:::
